use physics::constants
use units::si
use units::time
use units::astronomical

@name("Light-second")
@description("The distance that light travels in one second.")
@url("https://en.wikipedia.org/wiki/Light-second")
@aliases(lightseconds, lsec)
unit lightsecond: Length = speed_of_light × 1 s

@name("Lunar mass")
@description("The mass of Earth's Moon.")
@url("https://en.wikipedia.org/wiki/Moon#Size_and_mass")
unit lunar_mass: Mass = 7.342e22 kg

@name("Lunar radius")
@description("The radius of Earth's Moon.")
@url("https://en.wikipedia.org/wiki/Moon#Size_and_mass")
unit lunar_radius: Length = 1737.4 km

@name("Earth mass")
@description("The mass of planet Earth.")
@url("https://en.wikipedia.org/wiki/Earth")
unit earth_mass: Mass = 5.9722e24 kg

@name("Earth radius")
@description("The radius of planet Earth.")
@url("https://en.wikipedia.org/wiki/Earth")
unit earth_radius: Length = 6371.0088 km

@name("Earth's axial tilt")
@description("The angle between Earth's rotational axis and its orbital axis, also known as obliquity. This is the mean value for the J2000 epoch.")
@url("https://en.wikipedia.org/wiki/Earth")
let earth_axial_tilt: Angle = 23.439_281_1°

@name("Earth's orbital eccentricity")
@description("The eccentricity of Earth's orbit around the Sun at the J2000 epoch. The orbit is nearly circular.")
@url("https://en.wikipedia.org/wiki/Earth%27s_orbit")
let earth_orbital_eccentricity: Scalar = 0.016_708_634

@name("Earth's longitude of perihelion at J2000")
@description("The longitude of perihelion for Earth's orbit at the J2000 epoch, measured from the vernal equinox.")
@url("https://ssd.jpl.nasa.gov/planets/approx_pos.html")
let earth_longitude_of_perihelion_j2000: Angle = 102.937_348_1°

@name("Earth's perihelion longitude precession rate")
@description("The rate at which Earth's longitude of perihelion changes relative to the mean equinox. This combines apsidal precession (360°/112_000 years, caused by Jupiter and Saturn) and axial precession (360°/25_772 years, the precession of the equinoxes).")
@url("https://en.wikipedia.org/wiki/Apsidal_precession")
let earth_perihelion_precession_rate: Angle / Time = 360° / (112_000 years) + 360° / (25_772 years)

@name("Earth's mean anomaly at J2000")
@description("The mean anomaly of Earth's orbit at the J2000 epoch (2000-01-01 12:00 TT).")
@url("https://en.wikipedia.org/wiki/Mean_anomaly")
let earth_mean_anomaly_j2000: Angle = 357.529_11°

@name("Anomalistic year")
@description("The time between successive perihelion passages of Earth, approximately 365.259636 days. This is slightly longer than the tropical year due to the precession of Earth's perihelion.")
@url("https://en.wikipedia.org/wiki/Year#Sidereal,_tropical,_and_anomalistic_years")
@aliases(anomalistic_years)
unit anomalistic_year: Time = 365.259_636 days

@name("Mars mass")
@description("The mass of planet Mars.")
@url("https://en.wikipedia.org/wiki/Mars")
unit mars_mass: Mass = 6.4171e23 kg

@name("Mars radius")
@description("The radius of planet Mars.")
@url("https://en.wikipedia.org/wiki/Mars")
unit mars_radius: Length = 3389.5 km

@name("Jupiter mass")
@description("The mass of planet Jupiter.")
@url("https://en.wikipedia.org/wiki/Jupiter")
unit jupiter_mass: Mass = 1.89813e27 kg

@name("Jupiter radius")
@description("The radius of planet Jupiter.")
@url("https://en.wikipedia.org/wiki/Jupiter")
unit jupiter_radius: Length = 71_492 km

@name("Solar mass")
@description("The mass of the Sun.")
@url("https://en.wikipedia.org/wiki/Sun")
unit solar_mass: Mass = 1.98847e30 kg

@name("Solar radius")
@description("The radius of the Sun.")
@url("https://en.wikipedia.org/wiki/Sun")
unit solar_radius: Length = 6.957e5 km

dimension RadiantFlux = Power

@name("Solar luminosity")
@description("The total amount of energy emitted by the Sun per unit time.")
@url("https://en.wikipedia.org/wiki/Solar_luminosity")
unit solar_luminosity: RadiantFlux = 3.828e26 W

dimension SpectralFluxDensity = RadiantFlux / Area / Frequency

@name("Jansky")
@url("https://en.wikipedia.org/wiki/Jansky")
@aliases(janskys, Jy: short)
unit jansky: SpectralFluxDensity = 1e-26 W / m^2 / Hz

@name("Solar flux unit")
@url("https://en.wikipedia.org/wiki/Solar_flux_unit")
@aliases(solarfluxunits, sfu: short)
unit solarfluxunit: SpectralFluxDensity = 1e4 Jy
