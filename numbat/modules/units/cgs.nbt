use units::si::*

### Centimetre–gram–second system of units

@name("Dyne")
@url("https://en.wikipedia.org/wiki/Dyne")
@aliases(dyn)
pub unit dyne: Force = 1e-5 N

@name("Erg")
@url("https://en.wikipedia.org/wiki/Erg")
@aliases(ergs)
pub unit erg: Energy = 1 dyn cm

@name("Gauss")
@url("https://en.wikipedia.org/wiki/Gauss_(unit)")
pub unit gauss: MagneticFluxDensity = 100 µT

@name("Maxwell")
@url("https://en.wikipedia.org/wiki/Maxwell_(unit)")
@aliases(Mx)
pub unit maxwell: MagneticFlux = 1 gauss × cm^2

@name("Oersted")
@url("https://en.wikipedia.org/wiki/Oersted")
@metric_prefixes
@aliases(Oe: short)
pub unit oersted: MagneticFieldStrength = 1 / (4 pi) * dyne / maxwell

@name("Poise")
@url("https://en.wikipedia.org/wiki/Poise_(unit)")
@metric_prefixes
pub unit poise: DynamicViscosity = 1 dyn × s / cm^2

@name("Stokes")
@url("https://en.wikipedia.org/wiki/Stokes_(unit)")
@metric_prefixes
@aliases(St: short)
pub unit stokes: KinematicViscosity = cm^2 / s
