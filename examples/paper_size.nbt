# Compute ISO 216 paper sizes for the A series
#
# https://en.wikipedia.org/wiki/ISO_216

struct PaperSize {
    width: Length,
    height: Length,
}

impl PaperSize {
    fn area(self) -> Area = self.width × self.height

    fn to_string(self) -> String =
        "{self.width:>4} × {self.height:>5}   {self.area() -> cm²:>6.1f}"
}

fn paper_size_A(n: Scalar) -> PaperSize =
    if n == 0
        then PaperSize { width: 841 mm, height: 1189 mm }
        else PaperSize {
            width: floor_in(mm, paper_size_A(n - 1).height / 2),
            height: paper_size_A(n - 1).width,
        }

assert_eq(paper_size_A(4).width,  210 mm)
assert_eq(paper_size_A(4).height, 297 mm)
assert_eq(paper_size_A(4).area(), 62370 mm²)

fn row(n) = "A{n:<3}   {paper_size_A(n).to_string()}"

print("Name    Width     Height        Area  ")
print("----   -------   --------   ----------")
print(join(map(row, range(0, 10)), "\n"))
